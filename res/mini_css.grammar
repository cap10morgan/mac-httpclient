/**
    A Grammar for a small subset of CSS useful for syntax highlighting in an NSTextView + NSAttributedString
 */

@start        = ruleset*;
ruleset       = selectors '{' decls '}'.discard;
selectors     = selector commaSelector*;
selector      = LowercaseWord;            // forcing selectors to be lowercase words for use in a future syntax-highlight framework where i want that
commaSelector = ','.discard selector;
decls         = Empty | actualDecls;
actualDecls   = decl decl*;
decl          = property ':'.discard expr ';'.discard?;
property      = 'color' | 'background-color' | 'font-family' | 'font-size';
expr          = pixelValue | rgb | string | constants;
pixelValue    = Num 'px'.discard;
rgb           = 'rgb'.discard '(' Num ','.discard Num ','.discard Num ')'.discard;
string        = QuotedString;
constants     = 'bold' | 'normal' | 'italic';